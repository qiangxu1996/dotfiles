# https://github.com/romkatv/powerlevel10k/issues/57
if [[ $(ulimit -u) -lt 256 ]]; then
    GITSTATUS_NUM_THREADS=4
fi
# https://unix.stackexchange.com/questions/351466/set-a-default-resource-limit-for-all-users-with-systemd-cgroups
# https://www.kernel.org/doc/Documentation/cgroup-v1/pids.txt
cgroups_pid_file=/sys/fs/cgroup/user.slice/user-$UID.slice/pids.max
if [[ -r $cgroups_pid_file && $(cat $cgroups_pid_file) -lt 256 ]]; then
    GITSTATUS_NUM_THREADS=4
fi
unset cgroups_pid_file

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# https://github.com/zdharma-continuum/zinit#customizing-paths
declare -A ZINIT
ZINIT[PLUGINS_DIR]=$HOME/.local/share/zinit/plugins$zinit_suffix
# https://github.com/zdharma-continuum/zinit#using-zpfx-variable
ZPFX=$HOME/.local/share/zinit/polaris$zinit_suffix

### Added by Zinit's installer
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

### End of Zinit's installer chunk

HYPHEN_INSENSITIVE=true
zinit snippet OMZL::completion.zsh
zinit snippet OMZL::directories.zsh
zinit snippet OMZL::history.zsh
# http://zsh.sourceforge.net/Doc/Release/Options.html#History
unsetopt share_history
setopt inc_append_history_time
zinit snippet OMZL::key-bindings.zsh
# https://stackoverflow.com/questions/58187542/how-to-setup-vi-editing-mode-for-zsh
bindkey -v
# https://www.johnhawthorn.com/2012/09/vi-escape-delays/
KEYTIMEOUT=1

zinit ice depth"1"
zinit light romkatv/powerlevel10k

zinit light zsh-users/zsh-autosuggestions
zinit light zsh-users/zsh-completions

# to support colors in tab completion
# https://zdharma-continuum.github.io/zinit/wiki/LS_COLORS-explanation/
zinit ice atload'zstyle ":completion:*" list-colors "${(s.:.)LS_COLORS}"'
zinit light trapd00r/LS_COLORS

cmd_exist() {
    paths=$(which -a $1) && grep --invert-match zinit <<< "$paths" > /dev/null
}

zi ice \
    if"! cmd_exist dust && [[ $OSTYPE != darwin* ]]" \
    atclone"curl -O https://github.com/bootandy/dust/blob/master/completions/_dust" \
    atpull"%atclone" \
    from"gh-r" sbin"**/dust -> dust"
zi light bootandy/dust

# no darwin build and zinit will download the wrong binary
# https://github.com/zdharma-continuum/zinit/issues/575
zi ice \
    if"! cmd_exist eza && [[ $OSTYPE != darwin* ]]" \
    atclone"curl -O https://github.com/eza-community/eza/blob/main/completions/zsh/_eza" \
    atpull"%atclone" \
    from"gh-r" as"program"
zi light eza-community/eza
# https://github.com/ohmyzsh/ohmyzsh/blob/master/lib/directories.zsh
if command -v eza > /dev/null; then
    alias ls=eza
    alias lsa='eza -laa'
    alias l='eza -laa'
    alias ll='eza -l'
    alias la='eza -la'
fi

# https://github.com/zdharma-continuum/zinit/wiki/Recipes-for-popular-programs#exa
zi for \
    if'! cmd_exist fd' \
    atclone'cp -vf */autocomplete/_fd _fd' \
    atpull'%atclone' \
    from'gh-r' \
    sbin'**/fd -> fd' \
  @sharkdp/fd
# https://github.com/sharkdp/fd?tab=readme-ov-file#using-fd-with-fzf
export FZF_DEFAULT_COMMAND="fd --type file --follow --hidden --exclude .git --color=always"
export FZF_DEFAULT_OPTS="--ansi"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

zi ice if"! cmd_exist fzf" from"gh-r" as"program"
zi light junegunn/fzf
source <(fzf --zsh)

# https://github.com/zdharma-continuum/zinit#more-examples
zi ice if"! cmd_exist micromamba" from"gh-r" as"program" mv"micromamba* -> micromamba"
zi light mamba-org/micromamba-releases

# https://github.com/zdharma-continuum/zinit/wiki/Recipes-for-popular-programs#ripgrep
zi for \
    if'! cmd_exist rg' \
    from'gh-r' \
    sbin'**/rg -> rg' \
  BurntSushi/ripgrep

# both zinit and zoxide define zi
unalias zi
zinit ice if"! cmd_exist zoxide" from"gh-r" as"program"
zinit light ajeetdsouza/zoxide

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# https://gitlab.com/gnachman/iterm2/-/wikis/tmux-Integration-Best-Practices#how-do-i-use-shell-integration
export ITERM_ENABLE_SHELL_INTEGRATION_WITH_TMUX=YES
test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh" || true
unset ITERM_ENABLE_SHELL_INTEGRATION_WITH_TMUX

# add-to-the-end section
zinit light zsh-users/zsh-syntax-highlighting
eval "$(zoxide init zsh)"

# plugins' completions won't load without this
# https://github.com/zdharma-continuum/zinit?tab=readme-ov-file#calling-compinit-without-turbo-mode
autoload -Uz compinit
compinit

PATH="$HOME/.local/share/chezmoi/scripts:$PATH"
